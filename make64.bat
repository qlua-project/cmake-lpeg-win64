@echo off
if /I NOT "%1" == "/ENV" setlocal
call "C:\Program Files (x86)\Microsoft Visual Studio\2022\BuildTools\VC\Auxiliary\Build\vcvars64.bat"
if /I "%1" == "/ENV" goto EOF

set "RUNALLSTEPS="
set "CURRENT_DIR=%~dp0"

if /I "%1"=="/DEBUG" (
    set cmake_build_dir=cmake-build-debug-%VSCMD_ARG_HOST_ARCH%
    set CMAKE_BUILD_TYPE=Debug
    set STEP=%2
) else (
    set cmake_build_dir=cmake-build-release-%VSCMD_ARG_HOST_ARCH%
    set CMAKE_BUILD_TYPE=Release
    set STEP=%1
    if NOT "%2" == "" set STEP=%2
)
set CMAKE_INSTALL_PREFIX=%CURRENT_DIR:~0,-1%

if /I "%STEP%" == "/DUMP" goto DUMPBIN
if /I "%STEP%" == "/DUMPBIN" goto DUMPBIN
if /I "%STEP%" == "/CLEAR" goto CLEAN
if /I "%STEP%" == "/CLEAN" goto CLEAN
if /I "%STEP%" == "/DEL" goto DELETE
if /I "%STEP%" == "/DELETE" goto DELETE

goto CMAKESTEPS

:DUMPBIN
%~dp0dumpbin64.bat %cmake_build_dir%

goto END

:CLEAN
del /Q "%~dp0%cmake_build_dir%\CMakeCache.txt"

goto END

:DELETE
rmdir /s /q "%~dp0%cmake_build_dir%"

goto END

:CMAKESTEPS
mkdir "%~dp0%cmake_build_dir%"
pushd "%~dp0%cmake_build_dir%"

if /I "%STEP%" == "/SYSINFO" goto SYSINFO
if /I "%STEP%" == "/BUILD" goto BUILD
if /I "%STEP%" == "/INSTALL" goto INSTALL
if /I "%STEP%" == "" set RUNALLSTEPS=yes

:CONFIG
if NOT "%RUNALLSTEPS%" == "" echo - - - - - - - - - - - - - - - - - - - -
:: https://cmake.org/cmake/help/latest/manual/cmake.1.html#options
::  -G "NMake Makefiles" - to skip Visual Studio project files which are generated by default.
::  -Wdev                - Enable developer warnings that are meant for the author of the CMakeLists.txt files.
::  -L                   - This will effectively display current CMake settings.
::  --log-context        - message command outputting context attached to each message
rem cmake -G "NMake Makefiles" -Wdev --warn-uninitialized --log-context -L -B "%cmake_build_dir%" --install-prefix "%INSTALL_DIR:~0,-1%"
cmake -G "NMake Makefiles" -Wdev --warn-uninitialized --log-context -L ..

if ERRORLEVEL==1 goto END
if NOT "%RUNALLSTEPS%" == "" goto BUILD
goto END

:BUILD
if NOT "%RUNALLSTEPS%" == "" echo - - - - - - - - - - - - - - - - - - - -
cmake --build .

if ERRORLEVEL==1 goto END
if NOT "%RUNALLSTEPS%" == "" goto INSTALL
goto END

:INSTALL
if NOT "%RUNALLSTEPS%" == "" echo - - - - - - - - - - - - - - - - - - - -
cmake --install .

if ERRORLEVEL==1 goto END
rem if NOT "%RUNALLSTEPS%" == "" goto SYSINFO
goto END

:SYSINFO
:: https://cmake.org/cmake/help/latest/manual/cmake.1.html#cmdoption-cmake-system-information
::  --system-information [file] - it will dump additional information such as the cache, log files etc.
cmake -G "NMake Makefiles" --system-information "cmake-build-sysinfo.txt"
copy /y cmake-build-sysinfo.txt %~dp0

goto END

:END
popd

:ENDLOCAL
endlocal

:EOF
